// SHELL
output (char* buf, int n)  =>void COMMAND;

// SERIAL
output (int)      =>void SERIAL_BEGIN;
output (void)     =>void SERIAL_END;
output (byte)     =>void SERIAL_WRITE;
output (byte*,int)=>void SERIAL_WRITE_BUF;
input int SERIAL_READ;

native nohold _strlen(), _strncat(), _strncpy(), _strtol(), _itoa();
native do
    ##include <string.h>
    void boot_program_page (uint32_t page, uint8_t *buf);
    typedef typeof(boot_program_page)* boot_program_page_t;
end

call SERIAL_BEGIN => 9600;
finalize with
    call SERIAL_END;
end

#define CMD_SIZE   32
#define PAGE_SIZE 256

loop do
    var byte v = await SERIAL_READ;

    // FLASH WRITE
    if v == ':' then

        /* Intel hex format
         * : nn addr tp .... XX \n
         * ^                    :       (starting colon)
         *    ^                 nn      (number of bytes of data)
         *       ^              addr    (address to save)
         *           ^          tp      (record type)
         *               ^      ....    (data)
         *                   ^  XX      (checksum)
         * : 00 0000 01 FF              (last line)
         */

        /* scope of 256 bytes of data or tp="end" */

        var _uint8_t[PAGE_SIZE] mem;
        //var char[100] out;
        var int  mem_i = 0;
        var uint mem_addr;
        var bool checksum? = true;  // (sum+XX == 0) ? 1 : 0

        loop do
            /* scope of one line */

            var byte sum = 0;       // add nn+add+tp+...

            // LEN
            var u8 len;
            do
                var char[3] num;
                num[0] = await SERIAL_READ;
                num[1] = await SERIAL_READ;
                num[2] = '\0';
                len = _strtol(num,null,16);
                sum = sum + (byte)len;
                //_itoa(len, out, 16);
            end

            // ADDR
            do
                var char[5] num;
                num[0] = await SERIAL_READ;
                num[1] = await SERIAL_READ;
                num[2] = '\0';
                sum = sum + (byte)_strtol(num,null,16);
                num[2] = await SERIAL_READ;
                num[3] = await SERIAL_READ;
                num[4] = '\0';
                sum = sum + (byte)_strtol(&num[2],null,16);
                var u16 addr = _strtol(num,null,16);
                if mem_i == 0 then
                    // first line: set "mem_addr"
                    // TODO: assumes line addresses are contiguous
                    mem_addr = addr;
                end
                //_itoa(mem_addr,&out[_strlen(out)], 16);
            end

            // TYPE
            var u8 type;
            do
                var char[3] num;
                num[0] = await SERIAL_READ;
                num[1] = await SERIAL_READ;
                num[2] = '\0';
                type = _strtol(num,null,16);
                sum = sum + (byte)type;
                //_itoa(type,&out[_strlen(out)], 16);
            end

            // DATA
            loop i, len do
                var char[3] num;
                num[0] = await SERIAL_READ;
                num[1] = await SERIAL_READ;
                num[2] = '\0';
                var byte b = _strtol(num,null,16);
                sum = sum + (byte)b;
                if type == 0x00 then
                    mem[mem_i] = b;
                    mem_i = mem_i + 1;
                end
                //_itoa(mem[mem_i-1],&out[_strlen(out)], 16);
            end

            // CHECKSUM
            do
                var char[3] num;
                num[0] = await SERIAL_READ;
                num[1] = await SERIAL_READ;
                num[2] = '\0';
                sum = sum + (byte)_strtol(num,null,16);
                if (sum != 0) then
                    checksum? = false;  // all 256 will be ignored
                end
            end

            // "\n"
            await SERIAL_READ;

            if type==0x01 or mem_i==256 then
                /*
                 * WRITE TO FLASH:
                 * assumes (1) starting line address is aligned;
                 *         (2) line addresses are contiguous;
                 *         (3) lines never skip byte 256
                 */
                if checksum? and (mem_i > 0) then
                    var _boot_program_page_t f = (_boot_program_page_t) 0x7EC3;
                    _ISR_OFF();
                    f(mem_addr, mem)
                        finalize with nothing; end; // nohold
                    _ISR_ON();
                else
                    // no data (type==0x01 and there are no pending bytes)
                    if not checksum? then
                        call SERIAL_WRITE_BUF => ("checksum error\n",15);
                    end
                end

                break;      // finished .HEX or next line
            end

            // ":"
            await SERIAL_READ;
        end

    // NORMAL COMMAND
    else
        var char[CMD_SIZE] cmd;
        loop i, CMD_SIZE-2 do
            cmd[i] = v;
            if v == '\n' then
                cmd[i+1] = '\0';
                break;
            end
            v = await SERIAL_READ;
        end
        cmd[CMD_SIZE-2] = '\n';
        cmd[CMD_SIZE-1] = '\0';
        call COMMAND => (cmd,CMD_SIZE);
        call SERIAL_WRITE_BUF => (cmd,_strlen(cmd));
    end

end
