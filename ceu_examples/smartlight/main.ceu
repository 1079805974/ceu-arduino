#include "pwm.ceu"
#include "usart.ceu"
#include "out.ceu"
#include "wclock.ceu"
#include "adc.ceu"
#include "string.ceu"

native/pre do
    ##include "Wire.h"
end

var[20] byte write_buf = _; 
var[20] byte read_buf = _;

native _Wire;

spawn USART_Init(9600);

_Wire.begin();

/*
An util function parse byte to lightness 0 ~ 255.
    */
code/call ByteToLightness(var byte inValue) -> u8 
do
    if inValue >= 0 then
        escape inValue as u8;
    else
        escape (inValue + 256) as u8;
    end
end

/*
Set color by RGB number.

@param {u8} r red LED ligthness
@param {u8} g green LED ligthness
@param {u8} b blue LED ligthness

    */
code/await Set_Color (var u8 r, var u8 g, var u8 b) -> none 
do
    // spawn Pwm(3, 255 - r);
    // spawn Pwm(5, 255 - g);
    // spawn Pwm(6, 255 - b);
    emit PWM(3, 255 - r);
    emit PWM(5, 255 - g);
    emit PWM(6, 255 - b);
end

/*
Get the value from GY30 sensor. 

@return {int} lightness 0 ~ 65535

    */
code/await Read_Sensor (none) -> int
do
    var int value = _;
    {
        Wire.beginTransmission(0x23);
        Wire.write(0x10);
        Wire.endTransmission();
    }
    await 120ms;
    {   
        Wire.requestFrom(0x23, 2);
        for (@value = 0; Wire.available() >= 1; ) {
            char c = Wire.read();
            @value = (@value << 8) + (c & 0xFF);
        }
        @value = @value / 1.2;
    }
    escape value;
end

/*
Send the data in write_buf and clear it.
    */
code/await Send_Msg (none) -> none
do
    outer.write_buf = outer.write_buf .. [0];
    call String_Append_STR(&outer.write_buf, "\n");
    await USART_Tx(&outer.write_buf);
    outer.write_buf = [];
end

/*
Set LED color using read_buf and clear read_buf.
    */
code/await Handle_Msg(none) -> none
do
    var u8 r = call ByteToLightness(outer.read_buf[0]);
    var u8 g = call ByteToLightness(outer.read_buf[1]); 
    var u8 b = call ByteToLightness(outer.read_buf[2]);
    await Set_Color(r, g, b);
    outer.read_buf = [];
end

event none e;

par do
    loop do
        var int value = await Read_Sensor();
        write_buf = write_buf .. [0];
        call String_Append_INT(&write_buf, value, _);
        await Send_Msg();
        await 1s;
    end
with
    loop do
        await USART_Rx(&read_buf, 4);
        await Handle_Msg();
    end
end
