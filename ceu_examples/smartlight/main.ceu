#include "pwm.ceu"
#include "usart.ceu"
#include "out.ceu"
#include "wclock.ceu"
#include "adc.ceu"
#include "string.ceu"

native/pre do
    ##include "Wire.h"
end

var[50] byte write_buf = _;
var[50] byte read_buf = _;

native _Wire;

spawn USART_Init(9600);

_Wire.begin();

code/call ByteToLightness(var byte inValue) -> u8 
do
    if inValue >= 0 then
        escape inValue as u8;
    else
        escape (inValue + 256) as u8;
    end
end

code/await Set_Color (var u8 r, var u8 g, var u8 b) -> none 
do
    spawn Pwm(3, 255 - r);
    spawn Pwm(5, 255 - g);
    spawn Pwm(6, 255 - b);
end

code/call Read_Sensor (none) -> int
do
    var int value = _;
    {
        Wire.beginTransmission(0x23);
        Wire.write(0x10);
        Wire.endTransmission();
        delay(120);
        Wire.requestFrom(0x23, 2);
        for (@value = 0; Wire.available() >= 1; ) {
            char c = Wire.read();
            @value = (@value << 8) + (c & 0xFF);
        }
        @value = @value / 1.2;
    }
    escape value;
end

code/await Send_Msg (none) -> none
do
    outer.write_buf = outer.write_buf .. [0];
    call String_Append_STR(&outer.write_buf, "\n\r");
    await USART_Tx(&outer.write_buf);
    outer.write_buf = [];
end

code/await Handle_Msg(none) -> none
do
    var u8 r = call ByteToLightness(outer.read_buf[0]);
    var u8 g = call ByteToLightness(outer.read_buf[1]); 
    var u8 b = call ByteToLightness(outer.read_buf[2]);
    await Set_Color(r, g, b);
    outer.read_buf = [];
end

event none e;

loop do
    par/and do
        var int value = call Read_Sensor();
        write_buf = write_buf .. [0];
        call String_Append_INT(&write_buf, value, _);
        await Send_Msg();
        await 1s;
        emit e;
    with
        watching e do
            await USART_Rx(&read_buf, 4);
            await Handle_Msg();
        end
    end
end