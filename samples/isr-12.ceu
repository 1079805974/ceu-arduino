#include "arduino/isr/timer.ceu"
#include "arduino/isr/usart.ceu"
#include "arduino/isr/pin-02.ceu"

native/pre do
    ##define CEU_ARDUINO_SLEEP 1
end

var& IUsart usart;
vector[20] byte rx = [];
spawn Usart(9600, &rx) -> (&usart);

watching INT_PIN_02 do
    loop do
        par/and do
            await 1s;
        with
            vector[20] byte str = [].."Hello World!\n";
            await Usart_TX(&usart,&str);
        end
    end
end

await 1s;

watching INT_PIN_02 do
#if 1
    spawn do
        await async do
            loop do
                _delayMicroseconds(10000);
                //_delayMicroseconds(1000);
            end
        end
    end
#endif

    loop do
        if $rx == 0 then
            await usart.ok_rx;
        end
        vector[20] byte tx;
        atomic do
            tx = []..rx;
            rx = [];
        end
        await Usart_TX(&usart,&tx);
    end
end

// should tx up to 1-5
watching 6ms do
    vector[20] byte str = [].."12345\n67890";
    await Usart_TX(&usart, &str);
end

// print both in sequence
vector[20] byte str = [].."Hello World!\n";
par/and do
    await Usart_TX(&usart, &str);
with
    await Usart_TX(&usart, &str);
end

vector[20] byte str = [].."ok!\n";
await Usart_TX(&usart, &str);

await FOREVER;
